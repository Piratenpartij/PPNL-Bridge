#!/usr/bin/perl
use warnings;
use lib '/home/ruben/src/git/signal-irc-telegram-gateway';
use POSIX;
use POE qw(Component::Server::TCP);
use JSON;
use TOML;
use Hermod;

################################################################################
# Hermod server                                                                #
#                                                                              #
# Provides connectivity between relay bots for different chat platforms        #
# Uses Hermod.pm module for relaying incoming JSON messages to the configured  #
# bots.                                                                        #
#                                                                              #
# example incoming JSON format:                                                #
# {                                                                            #
#   "token": "xxxxxxxyyyyyyyzzzzzzz",                                          #
#   "chat": "signal",                                                          #
#   "user": "John Smith",                                                      #
#   "prefix": "sig",                                                           #
#   "text": "Hi there folks!",                                                 #
#   "file": "/path/to/file"                                                    #
# }                                                                            #
#                                                                              #
# All fields are mandatory, except "file"                                      #
#                                                                              #
# 2021, Ruben de Groot                                                         #
#                                                                              #
################################################################################

open my $fh, '<', "/etc/hermod.toml" or die "error opening configuration $!";
my ($cfg, $e) = from_toml do { local $/; <$fh> };
unless ($cfg) {
    die "Error parsing toml: $e";
}

# set process name
$0 = 'hermod';

POSIX::setsid or die "setsid: $!";
my $out = (defined $cfg->{common}{debug}) ? $cfg->{common}{debug} : '/dev/null';

my $pid = fork() // die $!; #//
exit(0) if $pid;
chdir "/";
umask 0;
open(STDIN,"</dev/null");
open(STDOUT,">>$out");
open(STDERR,">&STDOUT");

# daemon is now running
POE::Component::Server::TCP->new(
  Alias       => "hermod",
  Port        => (defined $cfg->{common}{port}) ? $cfg->{common}{port} : 31337,
  ClientInput => sub {
        my ($kernel, $heap,$input) = @_[KERNEL, HEAP, ARG0];
        print "Input: $input\n";
        $kernel->yield("shutdown");
        print handle($input,$cfg);
  },
);

# Start the server.
$poe_kernel->run();

sub handle {

    my ($input,$cfg) = @_;
    return unless $input =~ /^{.*}$/;
    my $msg;
    eval { $msg = decode_json $input; };
    if ($@) {
        print "Failed to decode $input: $@\n";
        return;
    }
    return "$input is not a hash\n" unless ref $msg eq "HASH";
    return "Missing fields\n" unless $msg->{user} and $msg->{prefix} and $msg->{text} and $msg->{token} and $msg->{chat};
    return "Token $msg->{token} mismatch\n" unless defined $msg->{token}
        and $msg->{token} eq $cfg->{common}{token};

    if ($msg->{file}) {
    } else {

        my $text;
        unless ($msg->{chat} eq "telegram") {
            ($text = $msg->{text}) =~ s/_/\\_/g;
            $text =~ s/\*/\\*/g;
            $text =~ s/\[/\\[/g;
            $text =~ s/\]/\\]/g;
            $text = '`['.$msg->{prefix}.'] '.$msg->{user}.':` '.$text."\n";
            Hermod::relay2Tel($text, $cfg->{telegram});
        }
        Hermod::relay2irc($msg->{text}, $cfg->{irc}, "[$msg->{prefix}] $msg->{user}: ") unless $msg->{chat} eq "irc";
        $text = "[$msg->{prefix}] $msg->{user}: $msg->{text}\n";
        Hermod::relay2mm($text, $cfg->{mattermost}) unless $msg->{chat} eq "mattermost";
        Hermod::relay2mtx($text, $cfg->{matrix}) unless $msg->{chat} eq "matrix";
        Hermod::relay2dis($text, $cfg->{discord}) unless $msg->{chat} eq "discord";
        Hermod::relay2sig($text, $cfg->{signal}) unless $msg->{chat} eq "signal";
    }
}
